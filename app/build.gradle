def versionMajor = 1
def versionMinor = 3
def versionPatch = 0
def versionBuild = {
    if (project.hasProperty("buildNumber")) {
        return project.buildNumber as int
    } else if (System.getenv("BUILD_NUMBER")) {
        return System.getenv("BUILD_NUMBER") as int
    } else {
        return 0
    }
}.call()

// adjust variable below to detect your CI builds
def isCIBuild = System.getenv()['bamboo_planName'] ? true : false

if (isCIBuild) {
    def gitSha = grgit.head().abbreviatedId
    def gitBranchName = grgit.branch.getCurrent().name
}
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
def projectName = "thunderboard"

// use your local.properties file to setup your own application keys and links
Properties properties = new Properties()
properties.load(project.rootProject.file('keystores/thunderboard.properties').newDataInputStream())
/*
 * Example for the firebase urls:
 *   cloud.data.url=https://fire-inferno-0001.firebaseio.com/
 *   cloud.demo.url=https://fire-inferno-0001.firebaseapp.com/#/
 *
 */
def cloudDataUrlDebug = properties.getProperty('cloud.data.url.debug')
def cloudDemoUrlDebug = properties.getProperty('cloud.demo.url.debug')
def cloudDataUrlRelease = properties.getProperty('cloud.data.url.release')
def cloudDemoUrlRelease = properties.getProperty('cloud.demo.url.release')

/* signing configs */
def sf = properties.getProperty('store.file')  // ../keystores/storeFile.jks
def sp = properties.getProperty('store.password')
def ka = properties.getProperty('key.alias')
def kp = properties.getProperty('key.password')

/* firebase key */
def keyFirebaseDebug = properties.getProperty('key.firebase.debug')
def keyFirebaseRelease = properties.getProperty('key.firebase.release')




buildscript {
    ext.android_support_version = '26.1.0'
    repositories {
        jcenter()
        google()
    }
    dependencies {
        println("android_studio= " + android_studio.toString())
        if (android_studio == 3){
            classpath 'com.android.tools.build:gradle:3.2.1'
            classpath 'com.google.gms:google-services:3.0.0'
        } else {
            classpath 'com.android.tools.build:gradle:2.3.3'
            classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        }
    }
}


//apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'de.felixschulze.gradle.hockeyapp'

android {
    compileSdkVersion 26
    signingConfigs {
        release {
            storeFile file("${sf}")
            storePassword "${sp}"
            keyAlias "${ka}"
            keyPassword "${kp}"
        }
    }

    defaultConfig {
        applicationId 'com.silabs.thunderboard'
        minSdkVersion 19
        targetSdkVersion 26
        versionCode versionMajor * 10000000 + versionMinor * 100000 + versionPatch * 1000 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        if (isCIBuild) {
            buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
            buildConfigField "String", "BRANCH_NAME", "\"${gitBranchName}\""
        }
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField 'String', 'MICROSITE_URL', "\"http://www.silabs.com/thunderboard\""
        //Android 3.0
        if (android_studio == 3){
            javaCompileOptions {
                annotationProcessorOptions {
                    includeCompileClasspath false
                }
            }
            multiDexEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        preDexLibraries = preDexEnabled
    }


    packagingOptions {
        // recommended from Firebase
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField 'String', 'CLOUD_DATA_URL', "\"${cloudDataUrlRelease}\""
            buildConfigField 'String', 'CLOUD_DEMO_URL', "\"${cloudDemoUrlRelease}\""
            buildConfigField 'String', 'KEY_FIREBASE', "\"${keyFirebaseRelease}\""
        }
        debug {
            buildConfigField 'String', 'CLOUD_DATA_URL', "\"${cloudDataUrlDebug}\""
            buildConfigField 'String', 'CLOUD_DEMO_URL', "\"${cloudDemoUrlDebug}\""
            buildConfigField 'String', 'KEY_FIREBASE', "\"${keyFirebaseDebug}\""
        }
    }

    // Rename the output apk
    applicationVariants.all { variant ->
        if (android_studio == 3){
            variant.outputs.all {
                outputFileName = isCIBuild ? "${projectName}-${variant.name}-${gitSha}.apk" : "${projectName}-${variant.name}.apk"
            }
        } else {
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                //don't use gitSha with dev builds because AS gets confused
                def filePrefix = isCIBuild ? "${projectName}-${variant.name}-${gitSha}" : "${projectName}-${variant.name}"
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    output.outputFile = file("$project.buildDir/${filePrefix}.apk")
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:multidex:1.0.2'
    // Android
    implementation "com.android.support:appcompat-v7:$android_support_version"
    implementation "com.android.support:recyclerview-v7:$android_support_version"
    implementation "com.android.support:cardview-v7:$android_support_version"
    implementation "com.android.support:mediarouter-v7:$android_support_version"
    implementation "com.android.support:palette-v7:$android_support_version"
    implementation "com.android.support:design:$android_support_version"
    // dagger
    implementation 'com.google.dagger:dagger:2.12'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.12'
    // Timber
    implementation 'com.jakewharton.timber:timber:4.5.1'
    // Butterknife
    implementation ('com.jakewharton:butterknife:8.8.1'){
        exclude module: 'support-compat'
    }
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    //annotation?
    implementation 'org.glassfish:javax.annotation:10.0-b28'
    // RxJava, RxAndroid
    implementation 'io.reactivex:rxjava:1.1.3'
    implementation 'io.reactivex:rxandroid:1.1.0'
    // Gson
    implementation 'com.google.code.gson:gson:2.8.0'
    // Firebase
    implementation 'com.firebase:firebase-client-android:2.5.2'
    // Retrofit
    implementation 'com.squareup.retrofit:retrofit:1.9.0'
    implementation 'com.squareup.okhttp:okhttp:2.5.0'
    // AltBeacon Android library
    implementation 'org.altbeacon:android-beacon-library:2.6.1@aar'
}

hockeyapp {
    apiToken = "${hockeyApiKey}"
    releaseType = 2 // alpha
    notify = 0
    status = 2
    notesType = 1
    notes = "Some notes."
    variantToApplicationId = [
            'release': "${hockeyIdAppRelease}",
            'debug'  : "${hockeyIdAppDebug}"
    ]
}

apply plugin: 'com.google.gms.google-services'
